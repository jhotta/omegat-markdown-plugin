project(":markdown-plugin") {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'findbugs'
    apply plugin: 'checkstyle'
    defaultTasks 'jar'

    configurations {
        provided
    }

    sourceSets {
        main { compileClasspath += configurations.provided }
        test
    }

    repositories {
        jcenter()
    }

    dependencies {
        compile 'org.pegdown:pegdown:1.6.0'
        provided 'commons-io:commons-io:2.4'
        provided project(":stub")
        testCompile 'org.testng:testng:6.9.10'
        testCompile 'commons-io:commons-io:2.4'
        testCompile project(":stub")
    }
    test.useTestNG()

    jar {
        from files(sourceSets.main.output.classesDir)
        from {configurations.compile.collect {zipTree(it)}} {
            exclude "META-INF/MANIFEST.MF"
        }
        manifest {
            attributes("OmegaT-Plugins": "tokyo.northside.omegat.MarkdownFilter")
        }
    }

    checkstyle {
        config = resources.text.fromFile("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
        ignoreFailures = true
        toolVersion = '7.1'
    }

    // Force prevent checkstyle/findbugs on Test.
    task checkstyleTest(overwrite: true) << {}
    task findbugsTest(overwrite: true) << {}

    findbugs {
        ignoreFailures = true
    }
    tasks.withType(FindBugs) {
        reports {
          xml.enabled = false
          html.enabled = true
        }
    }
}

project(":stub") {
    apply plugin: 'java'
}
